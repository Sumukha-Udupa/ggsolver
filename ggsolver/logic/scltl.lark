start: scltl_formula

?scltl_formula:     scltl_equivalence
?scltl_equivalence: scltl_implication (EQUIVALENCE scltl_implication)*
?scltl_implication: scltl_or (IMPLY scltl_or)*
?scltl_or:          scltl_and (OR scltl_and)*
?scltl_and:         scltl_until (AND scltl_until)*
?scltl_until:       scltl_unaryop (UNTIL scltl_unaryop)*

?scltl_unaryop:     scltl_always
             |     scltl_eventually
             |     scltl_next
             |     scltl_not
             |     scltl_wrapped

?scltl_always:      ALWAYS scltl_unaryop
?scltl_eventually:  EVENTUALLY scltl_unaryop
?scltl_next:        NEXT scltl_unaryop
?scltl_not:         NOT scltl_atom
?scltl_wrapped:     scltl_atom
             |     LSEPARATOR scltl_formula RSEPARATOR
?scltl_atom:        scltl_symbol
          |        scltl_true
          |        scltl_false

scltl_symbol: SYMBOL_NAME
scltl_true: pl_true
scltl_false: pl_false

// Operators must not be part of a word
UNTIL.2: "U"
ALWAYS.2: "G"
EVENTUALLY.2: "F"
NEXT.2: "X"

// Symbols cannot contain uppercase letters, because these are reserved
SYMBOL_NAME: /[a-z][a-z0-9_]*/

%ignore /\s+/

%import .pl.pl_true -> pl_true
%import .pl.pl_false -> pl_false
%import .pl.NOT -> NOT
%import .pl.OR -> OR
%import .pl.AND -> AND
%import .pl.EQUIVALENCE -> EQUIVALENCE
%import .pl.IMPLY -> IMPLY
%import .pl.LSEPARATOR -> LSEPARATOR
%import .pl.RSEPARATOR -> RSEPARATOR